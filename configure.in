AC_INIT(reparml,1.0)

AC_PROG_INSTALL

AC_ARG_ENABLE(local_stdlib,
    [  --enable-local-stdlib   use the in-sources standard library],,
    enable_local_stdlib=no)

AC_ARG_ENABLE(release,
    [  --enable-release   remove debugging output],,
    enable_release=no)

AC_ARG_ENABLE(mpi,
   [  --enable-mpi     enable MPI backend],,
   enable_mpi=no)

AC_PROG_OCAML
if test "$OCAMLC" = "no"; then
  AC_MSG_ERROR([Please install the OCaml compiler])
fi

case "$OCAMLVERSION" in
  0.*|1.*|2.*|3.0*)
     AC_MSG_ERROR(You need Objective Caml 3.10 or higher);;
esac

target=native
if test "$OCAMLOPT" = "no"; then
  target=byte
fi

AC_PROG_OCAMLLEX

AC_PROG_FINDLIB
if test "$OCAMLFIND" = "no"; then
  AC_MSG_ERROR([Please install OCaml findlib (the ocamlfind command)])
fi

OCAMLC_STDLIB=`$OCAMLFIND ocamlc -where`
OCAMLFIND_STDLIB=`$OCAMLFIND printconf stdlib`
if test "$OCAMLC_STDLIB" != "$OCAMLFIND_STDLIB"; then
  AC_MSG_ERROR([OCaml and OCamlfind make different assumptions about the standard library directory])
fi

AC_ARG_WITH([mpicc], AS_HELP_STRING([--with-mpicc=path], [Path of mpicc compiler]),
                     [MPICC=$with_mpicc],[])
AC_ARG_WITH([mpiexec], AS_HELP_STRING([--with-mpiexec=path], [Path of mpiexec launcher]),
                     [MPIEXEC=$with_mpiexec],[])

if test "$enable_mpi" = "yes"; then
  AX_MPI()
  AX_MPI_THREADS()
fi


AC_CHECK_OCAML_PKG([sdl])
 if test "$OCAML_PKG_sdl" = "no"; then
   AC_MSG_WARN([Ocamlsdl is not installed. You won't be able to run some examples.])
 fi

if test "$enable_local_stdlib" = "yes"; then
    stdlib_dir="$PWD/lib"
    rpmllib_dir="$PWD/interpreter/_build"
    rmlc="$PWD/compiler/rpmlc.byte"
    rmldep="$PWD/tools/rpmldep/rpmldep.byte"
    rmlbuild="$PWD/tools/rpmlbuild/rpmlbuild/_build/ocamlbuild.byte"
    rmlbuild_libdir="$PWD/tools/rpmlbuild/rpmlbuild/_build"
    mlmpi_libdir="$PWD/mpi"
else
    stdlib_dir="$prefix/lib/rpml"
    rpmllib_dir="$stdlib_dir"
    rmlc=rpmlc
    rmldep=rpmldep
    rmlbuild=rpmlbuild
    rmlbuild_libdir="$stdlib_dir/rpmlbuild"
    mlmpi_libdir="$stdlib_dir"
fi

AC_SUBST(INSTALL)
AC_SUBST(target)
AC_SUBST(stdlib_dir)
AC_SUBST(rpmllib_dir)
AC_SUBST(rmlc)
AC_SUBST(rmldep)
AC_SUBST(rmlbuild)
AC_SUBST(rmlbuild_libdir)
AC_SUBST(mlmpi_libdir)
AC_SUBST(enable_release)
AC_SUBST(enable_mpi)

AC_CONFIG_COMMANDS_PRE([m4/mkmyocamlbuild_config.sh "$prefix" "$rmlbuild_libdir" $OCAMLLIB])

AC_OUTPUT(config tools/rpmlbuild/rpmlbuild/rml_version.ml
                 compiler/utilities/version.ml test/bench/bench_version.ml)
